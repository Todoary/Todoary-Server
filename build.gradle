// QueryDSL 추가
buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    //querydsl 추가
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'com.todoary'
version = '0.0.1-SNAPSHOT'+new Date().format("yyyyMMddHHmmss")
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

springBoot {
    mainClass = 'com.todoary.ms.TodoaryApplication'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'junit:junit:4.13.1'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation('javax.xml.bind:jaxb-api')
    implementation('io.jsonwebtoken:jjwt-api:0.11.5')
    implementation('io.jsonwebtoken:jjwt-impl:0.11.5')
    implementation('io.jsonwebtoken:jjwt-jackson:0.11.5')
    implementation('com.nimbusds:nimbus-jose-jwt:3.10')
    implementation('org.bouncycastle:bcpkix-jdk15on:1.70')

    implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.4.1'

    // Security, Authentication
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-validation')

    // Oauth2
    implementation('org.springframework.boot:spring-boot-starter-oauth2-client:2.7.0')

    // Swagger
    implementation('io.springfox:springfox-boot-starter:3.0.0')
    // FireBase
    implementation 'com.google.firebase:firebase-admin:7.1.1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'

    // Slack
    implementation 'com.github.maricn:logback-slack-appender:1.4.0'

    // emoji 포함 길이
    implementation group: 'com.ibm.icu', name: 'icu4j', version: '63.1'

    // H2 데이터베이스
    implementation 'com.h2database:h2:2.1.214'

    // 스프링 시큐리티 테스트
    testImplementation('org.springframework.security:spring-security-test')

    // querydsl 이놈
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
}

tasks.named('test') {
    useJUnitPlatform()
}

// plain jar 생성하지 않음
jar {
    enabled = false
}

task copyMainResources {
    doLast {
        println '*********** main copy start ***********'
        copy {
            from './config'
            include '*.yml'
            into './src/main/resources'
        }
        println '*********** main copy end *************'
    }
}

task copyTestResources {
    doLast {
        println '*********** test copy start ***********'
        copy {
            from './config/application-local-h2.yml', './config/application.yml'
            into './src/test/resources'
        }
        println '*********** test copy end *************'
    }
}

processResources.dependsOn copyMainResources
processTestResources.dependsOn copyTestResources

//querydsl 추가
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
compileQuerydsl{
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

